%macro type3_MI_mixed(type3table);
*Identify Terms as Denomiator*;
data type3table;
*length Denominator $50;
set &type3table;
Denominator = trim(scan(ErrorTerm,2,'()'));
run;
proc freq data=type3table noprint;
where Denominator~=' ';
table Denominator /out=Denominator_name;
run;
*Identify Terms as Numerators*;
data error_set;
set type3table;
keep _imputation_ source df MS;
run;
proc sort data=error_set;
by source;
run;
data Denominator_name;
set Denominator_name;
rename Denominator=source;
run;
proc sort data=Denominator_name;
by source;
run;
*Merge Denominators to Each Error Source*;
data error_set;
merge error_set (in=in1) Denominator_name (in=in2);
by source;
if in2=1;
rename source=denominator DF=de_DF MS=MSE;
drop count percent;
run;
proc sort data=error_set;
by _imputation_ Denominator;
run;
proc sort data=type3table;
by _imputation_ Denominator;
run;
data type3table;
merge type3table error_set;
by _imputation_ Denominator;
label MSE='Mean Squared Error';
run;
*Adjust F-statistics by Multiple Imputation*;
data type3table;
set type3table;
An=1/MS;
Bn=1/(MS**2 * DF);
Ad=1/MSE;
Bd=1/(MSE**2 * de_DF);
run;
proc means data=type3table noprint;
class source;
output out=MIanalyze mean(An Bn Ad Bd)=ave_An ave_Bn ave_Ad ave_Bd var(An Ad)=ave_Cn
ave_Cd max(_Imputation_)=M;
run;
data MIanalyze;
set MIanalyze;
rn=2* ave_An**2 /( 2*ave_Bn+(M+1)*ave_Cn/M );
rd=2* ave_Ad**2 /( 2*ave_Bd+(M+1)*ave_Cd/M );
MI_F=ave_Ad/ave_An;
p_value=1-PROBF(MI_F,rn,rd);
run;
data finaloutput;
set MIanalyze;
where Source~=' ' and p_value~=.;
keep Source M rd rn MI_F p_value;
label M='# of Imputation' rn=DF rd='Error DF' MI_F='MI adjusted F' p_value='p-value';
run;
proc sort data=finaloutput;
by source;
run;
proc sort data=type3table;
by source;
run;
data finaloutput;
merge finaloutput type3table (where=(_imputation_=1) keep=_imputation_ source
ErrorTerm);
by source;
run;
proc print data= finaloutput label;
where p_value~=.;
var source M rn ErrorTerm rd MI_F p_value;
run;
%mend;
